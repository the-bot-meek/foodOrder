import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.container.DockerExecContainer

plugins {
    id("groovy")
    id("com.gradleup.shadow") version "8.3.9"
    id("io.micronaut.application") version "4.5.4"
    id("io.micronaut.aot") version "4.5.4"
    id("maven-publish")
}

version = "0.0.1"
group = "com.thebotmeek"

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':core')
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.validation:micronaut-validation-processor")
    implementation("io.micronaut.validation:micronaut-validation")
    implementation("jakarta.validation:jakarta.validation-api")
    runtimeOnly("ch.qos.logback:logback-classic")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    implementation("io.micronaut.security:micronaut-security-oauth2")
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.reactor:micronaut-reactor")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0'
    runtimeOnly 'com.fasterxml.jackson.module:jackson-modules-java8:2.20.0'
    aotPlugins("io.micronaut.security:micronaut-security-aot")
    compileOnly("io.micronaut:micronaut-http-client")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.yaml:snakeyaml")
    testImplementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut.views:micronaut-views-fieldset")
    implementation("io.micronaut.views:micronaut-views-thymeleaf")
    testImplementation 'net.bytebuddy:byte-buddy:1.17.7'
    testImplementation 'org.objenesis:objenesis:3.4'

    // I think this is needed as my controller unit tests Mock the DynamodbFacadeService.
    // toDo: refactor unit tests to Mock repository's as they should not know about the implementation of the repository's.
    implementation("software.amazon.awssdk:dynamodb-enhanced")
}


application {
    mainClass = "com.thebotmeek.api.Application"
}

micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("com.thebotmeek.api.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        configurationProperties.put("micronaut.security.jwks.enabled","false")
        configurationProperties.put("micronaut.security.openid-configuration.enabled","false")
    }
}


tasks.register('sourcesJar', Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.named("dockerfile") {
    baseImage("eclipse-temurin:21-jre-jammy")
}

// https://github.com/bmuschko/gradle-docker-plugin?tab=readme-ov-file
tasks.named("dockerBuild", DockerBuildImage) {
    images.set(["sharedfoodorder"])
}

tasks.register('integrationTest', Test) {
    group = "verification"
    doFirst {
        environment("requireIntegrationTests", "true")
        environment("AWS_REGION", "eu-west-1")
    }

    filter {
        includeTestsMatching("com.thebotmeek.api.integration.*")
    }
}



